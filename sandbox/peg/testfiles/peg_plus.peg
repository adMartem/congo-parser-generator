#########################
## Hierarchical Syntax ##
#########################

Grammar    <- Spacing (Definition)+ EndOfFile

Definition <- Identifier (LEFTARROW / COLON) Expression ';'?

Expression <- Choice / Sequence

Choice     <- Sequence (SLASH Sequence)+
Sequence   <- (Prefix ENTAILS?) +

Prefix     <- (AND / NOT)? Suffix
            / Cardinality

Suffix     <- Primary
            / Repetition

Repetition <- Primary (QUESTION / STAR / PLUS)

Cardinality <- CARD (CardinalityConstraint)?

Primary    <- Identifier !(LEFTARROW / COLON)
            / OPEN Expression CLOSE
            / Literal
            / Class
            / DOT


##########################
## Constraint Syntax    ##
##########################

CardinalityConstraint <- NATURAL ( (COMMA NATURAL) | PLUS )?

####################
## Lexical Syntax ##
####################

Identifier  <- (IdentStart IdentCont* / '<' Identifier '>') Spacing
IdentStart  <- [a-zA-Z_]
IdentCont   <- IdentStart / [0-9]

Literal     <- ['] (!['] Char)* ['] Spacing 
             / ["] (!["] Char)* ["] Spacing 

Class       <- '[' (!']' Range )* ']' Spacing
Range       <- Char '-' Char / Char
Char        <- '\\' [nrt"'\[\]\\]
             / '\\' [0-2] [0-7] [0-7]
             / '\\' [0-7] [0-7]? 
             / !'\\' .

LEFTARROW   <- '<-' Spacing
COLON       <- ':' Spacing
SLASH       <- ('/' / '|') Spacing
AND         <- '&' Spacing
NOT         <- '!' Spacing
QUESTION    <- '?' Spacing
STAR        <- '*' Spacing
PLUS        <- '+' Spacing
OPEN        <- '(' Spacing
CLOSE       <- ')' Spacing
DOT         <- '.' Spacing

Spacing     <- ( Space / Comment )*
Comment     <- '#' (!EndOfLine .)* EndOfLine
Space       <- ' ' / '\t' / EndOfLine
EndOfLine   <- '\r\n' / '\n' / '\r'
EndOfFile   <- !.


#######################
## Extension Syntax  ##
#######################

ENTAILS     <- '>>>' Spacing
CARD        <- '%' Spacing / !Comment '#'

NATURAL     <- '0' / [1-9] [0-9]* Spacing
COMMA       <- ',' Spacing